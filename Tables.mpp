partition BIRTH_AGES
{
	10, 15, 20, 25, 30, 35, 40, 50, 60
};

partition INFECTION_AGES
{
	0,1,2,3,4,5,10,20,30,40,50,75,100
};

table Child DurationOfLifeChild //EN Duration of Life Child
{
	{
		value_in(alive),				//EN Population size
		min_value_out(duration()),		//EN Minimum duration of life decimals=4
		max_value_out(duration()),		//EN Maximum duration of life decimals=4
		duration() / value_in(alive)	//EN Life expectancy decimals=4
	}	//EN Demographic characteristics
};

table Child MotherEventAgeHist_fm //EN Age of child at mother events
{
	self_scheduling_split(age, INFECTION_AGES)+
	*
	{
		entrances(alive, FALSE),
		entrances(mother_disease_status_fm, INFECTED)
	}
};

table Child MotherEventCount_fm //EN Mother event experienced by children
{
	mother_disease_status_fm
	*
	{
		unit
	}
};

table Mother MotherEventAges //EN Avg Age of mother at events 
{
	{
		event(value_at_entrances(disease_status, INFECTED, age)) / event(entrances(disease_status, INFECTED)), //EN Age at Infection
		event(value_at_entrances(showing_symptoms, TRUE, age)) / event(entrances(showing_symptoms, TRUE)), //EN Age at Symptoms
		event(value_at_entrances(disease_status, TREATED, age)) / event(entrances(disease_status, TREATED)), //EN Age at Treatment
		event(value_at_entrances(disease_status, DEAD, age)) / event(entrances(disease_status, DEAD)) //EN Age at Death
	}
};

table Mother MotherEventHist //EN Histogram of mother age at events
{
	self_scheduling_split(age, INFECTION_AGES)+
	*
	{
		entrances(disease_status, INFECTED), //EN Age at Infection
		entrances(showing_symptoms, TRUE), //EN Age at Symptoms
		entrances(disease_status, TREATED), //EN Age at Treatment
		entrances(disease_status, DEAD) //EN Age at Death
	}
};

table Mother ageAtBirth //EN Age of mother at birth of children
{
	self_scheduling_split(age, BIRTH_AGES)+ //EN Mother Age
	*
	{
		changes(number_children) //EN Number of Births
	}
};

table Mother FertDetails //EN Number of children per mother
[alive == FALSE]
{
	number_children+ //EN Number of Children
	*
	{
		unit //EN Frequency
	}	//EN Demographic characteristics
};

table Mother OutOfHealthy //EN Out Of Healthy
{
	self_scheduling_split(duration(disease_status_detailed, HEALTHY_), BIRTH_AGES)+ //EN Mother Age
	*
	{
		unit,
		undergone_transition(disease_status_detailed, HEALTHY_, DEAD_),
		undergone_transition(disease_status_detailed, HEALTHY_, INFECTED_),
		exits(disease_status_detailed, HEALTHY_)
	}
};

table Mother OutOfInfected //EN Out Of Infected
{
	self_scheduling_split(duration(disease_status_detailed, INFECTED_), INFECTION_AGES)+ //EN Time since infection
	*
	{
		unit,
		undergone_transition(disease_status_detailed, INFECTED_, DEAD_),
		undergone_transition(disease_status_detailed, INFECTED_, SYMPTOMS_),		
		undergone_transition(disease_status_detailed, INFECTED_, DIAGNOSED_),		
		exits(disease_status_detailed, INFECTED_)
	}
};

table Mother OutOfSymptoms //EN Out Of Symptoms
{
	self_scheduling_split(duration(disease_status_detailed, SYMPTOMS_), INFECTION_AGES)+ //EN Time since Symptoms
	*
	{
		unit,
		undergone_transition(disease_status_detailed, SYMPTOMS_, DEAD_),		
		undergone_transition(disease_status_detailed, SYMPTOMS_, TREATED_),		
		exits(disease_status_detailed, SYMPTOMS_)
	}
};

table Mother OutOfDiagnosed //EN Out Of Diagnosis
{
	self_scheduling_split(duration(disease_status_detailed, DIAGNOSED_), INFECTION_AGES)+ //EN Time since diagnosis
	*
	{
		unit,
		undergone_transition(disease_status_detailed, DIAGNOSED_, DEAD_),	
		undergone_transition(disease_status_detailed, DIAGNOSED_, SYMPTOMS_),		
		undergone_transition(disease_status_detailed, DIAGNOSED_, TREATED_),		
		exits(disease_status_detailed, DIAGNOSED_)
	}
};

table Mother OutOfTreated //EN Out Of Treated
{
	self_scheduling_split(duration(disease_status_detailed, TREATED_), INFECTION_AGES)+ //EN Time since first treatment
	*
	{
		unit,
		undergone_transition(disease_status_detailed, TREATED_, DEAD_),
		exits(disease_status_detailed, TREATED_)
	}
};

table Child OutOfNotInSchool //EN Out Of Not In School
{
	integer_age+ //EN Time since birth
	*
	{
		unit,
		undergone_transition(school_status, NOT_IN_SCHOOL, IN_SCHOOL),
		exits(school_status, NOT_IN_SCHOOL)
	}
};

table Child OutOfInSchool //EN Out Of In School
{
	integer_age+ //EN Time since school start
	*
	{
		unit,
		undergone_transition(school_status, IN_SCHOOL, GRADUATED),
		undergone_transition(school_status, IN_SCHOOL, DROPOUT),
		exits(school_status, IN_SCHOOL)
	}
};

table Child MDSDdropout //EN MDSD_dropout
{
	integer_age+ //EN Time since school start
	*
	{
		unit,
		undergone_transition(school_status, IN_SCHOOL, GRADUATED),
		undergone_transition(school_status, IN_SCHOOL, DROPOUT),
		undergone_transition(mother_dsd_at_dropout, HEALTHY_, INFECTED_),
		undergone_transition(mother_dsd_at_dropout, HEALTHY_, DIAGNOSED_),
		undergone_transition(mother_dsd_at_dropout, HEALTHY_, SYMPTOMS_),
		undergone_transition(mother_dsd_at_dropout, HEALTHY_, TREATED_),
		undergone_transition(mother_dsd_at_dropout, HEALTHY_, DEAD_),
		undergone_transition(school_status, IN_SCHOOL, DROPOUT) - 
			(undergone_transition(mother_dsd_at_dropout, HEALTHY_, INFECTED_) +
			undergone_transition(mother_dsd_at_dropout, HEALTHY_, DIAGNOSED_) +
			undergone_transition(mother_dsd_at_dropout, HEALTHY_, SYMPTOMS_) +
			undergone_transition(mother_dsd_at_dropout, HEALTHY_, TREATED_) +
			undergone_transition(mother_dsd_at_dropout, HEALTHY_, DEAD_)) // EN Healthy
	}
};

///////////////////////////////////////////////////////////////
// Table Groups

table_group CHILD
{
	DurationOfLifeChild, MotherEventAgeHist_fm, MotherEventCount_fm
};

table_group MOTHER
{
	MotherEventAges, MotherEventHist, ageAtBirth, FertDetails
};

table_group M_OUT_OF_TABLES
{
	OutOfHealthy, OutOfInfected, OutOfSymptoms, OutOfDiagnosed, OutOfTreated
};

table_group C_OUT_OF_TABLES
{
	OutOfNotInSchool, OutOfInSchool
};